plugins {
    id 'idea'
    id 'net.researchgate.release' version '2.3.5'
}

allprojects {
    apply plugin: 'java'

    repositories {
        jcenter()
    }

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }

    task publish {
        println "Publishing $jar.archiveName archive to your central repository"
    }
}

release {
    tagTemplate = '$name-$version'
}

// Here we define our own tasks to invoke the release plugin tasks.
// This is to support multi module project releases. We only want to touch the scm once.
// We also override the release task, in order to execute our new tasks.

task prepareRelease(dependsOn: [
    createScmAdapter,
    initScmAdapter,
    checkCommitNeeded,
    checkUpdateNeeded,
    unSnapshotVersion,
    confirmReleaseVersion, 
    checkSnapshotDependencies,
    preTagCommit,
]) {}

task performRelease(type: GradleBuild, dependsOn: prepareRelease) {
    tasks = ['publish']
    // Gradle knows which sub projects will have the 'publish' task, and add them into the task graph for us.
    // It will also work out the dependencies between projects as normal, so the order of build/publish is handled for us.
}

task finalizeRelease(dependsOn: [
    createScmAdapter,
    initScmAdapter,
    createReleaseTag,
    updateVersion,
    commitNewVersion
]) {
    mustRunAfter performRelease
}

createReleaseTag.mustRunAfter initScmAdapter

task release(overwrite: true, dependsOn: [performRelease, finalizeRelease]) {}

wrapper.gradleVersion = '2.10'